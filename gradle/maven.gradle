apply plugin: 'maven'

install {
    customizePom(repositories.mavenInstaller.pom, project)
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { p ->
        // sort to make pom dependencies order consistent to ease comparison of older poms
        p.dependencies = p.dependencies.sort { dep ->
            "$dep.scope:$dep.optional:$dep.groupId:$dep.artifactId"
        }
    }
    pom.project {
        name gradleProject.name
        packaging "war"
        description gradleProject.name
        organization {
            name 'Spring IO'
            url 'http://spring.io/'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/rwinch/springdata-gateway-drug'
            connection 'scm:git:git://github.com/github.com/rwinch/springdata-gateway-drug'
            developerConnection 'scm:git:git://github.com/rwinch/springdata-gateway-drug'
        }
        developers {
            developer {
                id 'rwinch'
                name 'Rob Winch'
            }
        }
        properties {
            'm2eclipse.wtp.contextRoot' '/'
        }
        repositories {
            repository {
                id 'spring-milestone'
                url 'http://repo.spring.io/snapshot'
            }
        }
        build {
            plugins {
                plugin {
                    groupId 'org.apache.maven.plugins'
                    artifactId 'maven-compiler-plugin'
                    configuration {
                        source '1.7'
                        target '1.7'
                    }
                }
                plugin {
                    groupId 'org.apache.maven.plugins'
                    artifactId 'maven-war-plugin'
                    version '2.3'
                    configuration {
                        failOnMissingWebXml 'false'
                    }
                }

                plugin {
                    groupId 'com.mysema.maven'
                    artifactId 'apt-maven-plugin'
                    version '1.1.1'
                    executions {
                        execution {
                            goals {
                              goal 'process'
                            }
                            configuration {
                              outputDirectory 'target/generated-sources/java'
                              processor 'com.mysema.query.apt.jpa.JPAAnnotationProcessor'
                            }
                        }
                    }
                }
            }
        }
    }
}

task generatePom {
    group = 'Build'
    description = 'Generates a Maven pom.xml'

    ext.generatedPomFileName = "pom.xml"
    onlyIf { install.enabled }

    inputs.files(fileTree(project.rootProject.rootDir).include("**/*.gradle").files)
    inputs.files(new File(project.rootProject.rootDir, Project.GRADLE_PROPERTIES))
    outputs.files(generatedPomFileName)

    doLast() {
        def p = pom {}
        customizePom(p, project)
        p.writeTo(generatedPomFileName)
    }
}

build.dependsOn generatePom